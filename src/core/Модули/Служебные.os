#Использовать fs

Перем Лог;

Функция ЭтоЧисло(Строка) Экспорт
	Попытка
		Число = Число(Строка);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции

Функция ВыполнитьКоманду(КоманднаяСтрока, ТекущийКаталог = ".", ТипВывода = "Строка") Экспорт
	
	Лог.Отладка("Запускаю команду: %1", КоманднаяСтрока);

	Процесс = СоздатьПроцесс(КоманднаяСтрока, ТекущийКаталог, Истина, , КодировкаТекста.UTF8);
	Процесс.Запустить();
	Процесс.ОжидатьЗавершения();
	
	мСтроки = Новый Массив();
	Пока Процесс.ПотокВывода.ЕстьДанные Цикл
		СтрокаВывода = Процесс.ПотокВывода.ПрочитатьСтроку();
		Если ТипВывода = "Консоль" Тогда
			Сообщить(СтрокаВывода);
		Иначе
			мСтроки.Добавить(СтрокаВывода);
		КонецЕсли;
	КонецЦикла;

	Если ТипВывода = "Массив" Тогда
		Возврат мСтроки;
	ИначеЕсли ТипВывода = "Строка" Тогда
		Возврат СтрСоединить(мСтроки, Символы.ПС);
	КонецЕсли;

КонецФункции

Функция ИсполнитьКоманду(Знач СтрокаВыполнения, РабочийКаталог = "") Экспорт
	
	СтрокаВыполнения = СтрЗаменить(СтрокаВыполнения, Символы.ПС, " ");

	Команда = Новый Команда;
	
	Если ЗначениеЗаполнено(РабочийКаталог) Тогда
		Команда.УстановитьРабочийКаталог(РабочийКаталог);
	КонецЕсли;

	Команда.ПоказыватьВыводНемедленно(Истина);
	
	Сообщить("Выполняю команду:");
	Сообщить(СтрокаВыполнения);
	
	Команда.УстановитьСтрокуЗапуска(СтрокаВыполнения);
	
	КодВозврата = Команда.Исполнить();
	
	Возврат КодВозврата;

КонецФункции

Функция ЗаписатьВоВременныйФайл(Текст, Расширение = "list") Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	
	ЗаписатьВФайл(ИмяВременногоФайла, Текст);
	
	Возврат ИмяВременногоФайла;

КонецФункции

Процедура ЗаписатьВФайл(ИмяФайла, Текст) Экспорт
	
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.UTF8NoBOM);
	ЗаписьТекста.Записать(Текст);
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Функция ПрочитатьИзФайла(ИмяФайла, вМассив = Ложь) Экспорт

	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8NoBOM);
	Текст = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	Если вМассив Тогда
		Возврат ТекстВМассив(Текст);
	Иначе
		Возврат Текст;
	КонецЕсли;

КонецФункции

Функция ТекстВМассив(Текст) Экспорт
	мТекст = Новый Массив();
	Для НомерСтроки=1 По СтрЧислоСтрок(Текст) Цикл
		мТекст.Добавить(СтрПолучитьСтроку(Текст, НомерСтроки));
	КонецЦикла;
	Возврат мТекст;
КонецФункции

Функция ОбернутьВКавычки(Знач Строка) Экспорт
	Если Лев(Строка, 1) = """" И Прав(Строка, 1) = """" Тогда
		Возврат Строка;
	Иначе
		Возврат """" + Строка + """";
	КонецЕсли;
КонецФункции

Процедура УбратьКонцевойСлеш(Путь) Экспорт
	ПоследнийСлеш = Прав(Путь, 1);
	Если ПоследнийСлеш = "/" Или ПоследнийСлеш = "\" Тогда
		Путь = Лев(Путь, СтрДлина(Путь) - 1);
	КонецЕсли;
КонецПроцедуры

Процедура ПривестиКСлешамСистемы(Путь) Экспорт
	СистемныйСлеш = ПолучитьРазделительПути();
	Если СистемныйСлеш = "/" Тогда
		Путь = СтрЗаменить(Путь, "\", СистемныйСлеш);
	ИначеЕсли СистемныйСлеш = "\" Тогда
		Путь = СтрЗаменить(Путь, "/", СистемныйСлеш);
	КонецЕсли;
КонецПроцедуры

Лог = ПараметрыПриложения.Лог();