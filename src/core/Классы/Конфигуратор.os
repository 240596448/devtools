#Использовать v8runner

Перем Конфигуратор;

Процедура ПриСозданииОбъекта(Настройки)

	Конфигуратор = Новый УправлениеКонфигуратором();

	База = Настройки.База;

	ПараметрыСтрокиСоединения = Конфигуратор.ПараметрыСтрокиСоединения();
	ПараметрыСтрокиСоединения.ПутьКФайлуБазы = База.ПутьКФайлуБазы;
	ПараметрыСтрокиСоединения.Сервер         = База.Сервер;
	ПараметрыСтрокиСоединения.Порт           = База.Порт;
	ПараметрыСтрокиСоединения.ИмяБазы        = База.Имя;
	
	Конфигуратор.УстановитьКонтекст(ПараметрыСтрокиСоединения, База.Пользователь, База.Пароль);

	ПутьКПлатформе = Настройки.ПутьКПлатформе;
	Если ЗначениеЗаполнено(ПутьКПлатформе) Тогда
		Конфигуратор.ПутьКПлатформе1С(ПутьКПлатформе);
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьИмяФайлаСообщенийПлатформы(Путь) Экспорт
	Конфигуратор.УстановитьИмяФайлаСообщенийПлатформы(Путь);
КонецПроцедуры

Процедура ЗагрузитьКонфигурациюИзФайлов(КаталогИсходников, 
										ИмяФайлаСпискаОбъектов, 
										ОбновитьФайлВерсий = Ложь, 
										ОбновитьКонфигурациюИБ = Ложь) Экспорт
	
	Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(
		КаталогИсходников, 
		ИмяФайлаСпискаОбъектов, 
		, 
		ОбновитьФайлВерсий, 
		ОбновитьКонфигурациюИБ);

КонецПроцедуры

Процедура ВыгрузитьКонфигурациюВФайлы(КаталогИсходников) Экспорт
	Конфигуратор.ВыгрузитьКонфигурациюВФайлы(КаталогИсходников, , Истина);
КонецПроцедуры

Процедура ВыгрузитьОбъектыКонфигурациВФайлы(КаталогИсходников, ИмяФайлаСпискаОбъектов) Экспорт

	Параметры = Конфигуратор.СтандартныеПараметрыЗапускаКонфигуратора();
	Параметры.Добавить(СтрШаблон("/DumpConfigToFiles %1", Служебные.ОбернутьВКавычки(КаталогИсходников)));
	Параметры.Добавить(СтрШаблон("-format %1", "Hierarchical"));

	Параметры.Добавить(СтрШаблон("-listFile %1", Служебные.ОбернутьВКавычки(ИмяФайлаСпискаОбъектов)));

	Параметры.Добавить("-update");
	Параметры.Добавить("-force");

	Конфигуратор.ВыполнитьКоманду(Параметры);

КонецПроцедуры

Процедура ЗахватитьОбъектыВХранилище(СписокОбъектовХранилища) Экспорт
	
	ШаблонФайла = 
		"<Objects xmlns=""http://v8.1c.ru/8.3/config/objects"" version=""1.0"">
		|%1
		|</Objects>";
	ШаблонОбъекта = "	<Object fullName = ""%1"" includeChildObjects= ""true"" />";

	мОбъекты = Новый Массив();
	Для каждого Стр Из СписокОбъектовХранилища Цикл
		мОбъекты.Добавить(СтрШаблон(ШаблонОбъекта, Стр));
	КонецЦикла;

	Текст = СтрШаблон(ШаблонФайла, СтрСоединить(мОбъекты, Символы.ПС));
	
	ИмяФайлаСпискаОбъектовХранилища = Служебные.ЗаписатьВоВременныйФайл(Текст, "objects");

	Хранилище = ПараметрыПриложения.ПолучитьПараметр("Настройки").Хранилище;

	Конфигуратор.ЗахватитьОбъектыВХранилище(
		Хранилище.Путь, 
    	Хранилище.Пользователь,
		Хранилище.Пароль,
		ИмяФайлаСпискаОбъектовХранилища);

	УдалитьФайлы(ИмяФайлаСпискаОбъектовХранилища);

КонецПроцедуры