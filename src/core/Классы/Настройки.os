#Использовать strings
#Использовать fs

Перем ПутьКПлатформе Экспорт;
Перем База Экспорт;
Перем Хранилище Экспорт;
Перем Репозиторий Экспорт;

Перем ИмяНастройки Экспорт;

Процедура ОбработкаПолученияПредставления(Строка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	Шаблон = 
	"Настройки:
	| - Путь к платформе: %1
	| - База: %2
	| - Хранилище: %3
	| - Репозиторий: %4";

	Строка = СтрШаблон(Шаблон, ПутьКПлатформе, База, Хранилище, Репозиторий);

КонецПроцедуры

Процедура ПриСозданииОбъекта(ПутьКФайлуСНастройками = "")

	Лог = ПараметрыПриложения.Лог();

	Если НЕ ЗначениеЗаполнено(ПутьКФайлуСНастройками) Тогда
		Лог.КритичнаяОшибка("Не задан путь к файлу конфигурации (опция --config)");
		ВызватьИсключение "";
	КонецЕсли;

	Если НЕ ФС.ФайлСуществует(ПутьКФайлуСНастройками) Тогда
		Лог.КритичнаяОшибка("Файл конфигурации не существует (опция --config): %1", ФС.ПолныйПуть(ПутьКФайлуСНастройками));
		ВызватьИсключение "";
	КонецЕсли;		

	Инициализация();

	Лог.Информация("Читаем файл настроек %1", ПутьКФайлуСНастройками);
	Попытка
		Параметры = СтроковыеФункции.ПрочитатьФайлJSON(ПутьКФайлуСНастройками);
	Исключение
		Лог.КритичнаяОшибка("Не удалось прочитать файл json: %1", ПутьКФайлуСНастройками);
	КонецПопытки;

	Если НЕ ТипЗнч(Параметры) = Тип("Структура") Тогда
		Лог.КритичнаяОшибка("Файл настроек не является структурой: %1", ПутьКФайлуСНастройками);
		ВызватьИсключение "";
	КонецЕсли;

	Если Параметры.Свойство("ПутьКПлатформе") Тогда
		ПутьКПлатформе = Параметры.ПутьКПлатформе;
		Лог.Информация("Устанавливаем путь к платформе: %1", ПутьКПлатформе);
	Иначе
		Лог.Информация("Путь к платформе: <не задан>");
	КонецЕсли;

	Если Параметры.Свойство("База") Тогда
		База = Новый База(Параметры.База);
	КонецЕсли;
	Лог.Информация("База: %1", База);

	Если Параметры.Свойство("Хранилище") Тогда
		Хранилище = Новый Хранилище(Параметры.Хранилище);
	КонецЕсли;
	Лог.Информация("Хранилище: %1", Хранилище);

	Если Параметры.Свойство("Репозиторий") Тогда
		Репозиторий = Новый Репозиторий(Параметры.Репозиторий);
	КонецЕсли;
	Лог.Информация("Репозиторий: %1", Репозиторий);

	Файл = Новый Файл(ПутьКФайлуСНастройками);
	ИмяНастройки = Файл.ИмяБезРасширения;

КонецПроцедуры

Процедура Инициализация()

	ПутьКПлатформе = "";
	База        = Новый База();
	Хранилище   = Новый Хранилище();
	Репозиторий = Новый Репозиторий();

КонецПроцедуры