#Использовать strings
#Использовать fs

Перем ПутьКПлатформе Экспорт;
Перем База Экспорт;
Перем Хранилище Экспорт;
Перем Репозиторий Экспорт;

Перем ИмяНастройки Экспорт;

Процедура ОбработкаПолученияПредставления(Строка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	Шаблон = 
	"Настройки:
	| - Путь к платформе: %1
	| - База: %2
	| - Хранилище: %3
	| - Репозиторий: %4";

	Строка = СтрШаблон(Шаблон, ПутьКПлатформе, База, Хранилище, Репозиторий);

КонецПроцедуры

Процедура ПриСозданииОбъекта(ПутьКФайлуСНастройками = "")

	Лог = ПараметрыПриложения.Лог();

	Если НЕ ЗначениеЗаполнено(ПутьКФайлуСНастройками) Тогда
		Лог.КритическаяОшибка("Не задан путь к файлу конфигурации (опция --config)");
	КонецЕсли;

	Если НЕ ФС.ФайлСуществует(ПутьКФайлуСНастройками) Тогда
		Лог.КритическаяОшибка("Файл конфигурации не существует (опция --config): %1", ФС.ПолныйПуть(ПутьКФайлуСНастройками));
	КонецЕсли;		

	Инициализация();

	Лог.Информация("Читаем файл настроек %1", ПутьКФайлуСНастройками);
	Попытка
		Параметры = СтроковыеФункции.ПрочитатьФайлJSON(ПутьКФайлуСНастройками);
	Исключение
		Лог.КритическаяОшибка("Не удалось прочитать файл json: %1", ПутьКФайлуСНастройками);
	КонецПопытки;

	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		Если Параметры.Свойство("ПутьКПлатформе") Тогда
			ПутьКПлатформе = Параметры.ПутьКПлатформе;
			Лог.Информация("Устанавливаем путь к платформе: %1", ПутьКПлатформе);
		КонецЕсли;
		Если Параметры.Свойство("База") Тогда
			База = Новый База(Параметры.База);
			Лог.Информация("%1", База);
		КонецЕсли;
		Если Параметры.Свойство("Хранилище") Тогда
			Хранилище = Новый Хранилище(Параметры.Хранилище);
			Лог.Информация("%1", Хранилище);
		КонецЕсли;
		Если Параметры.Свойство("Репозиторий") Тогда
			Репозиторий = Новый Репозиторий(Параметры.Репозиторий);
			Лог.Информация("%1", Репозиторий);
		КонецЕсли;

		Файл = Новый Файл(ПутьКФайлуСНастройками);
		ИмяНастройки = Файл.ИмяБезРасширения;
		
	Иначе
		Лог.КритическаяОшибка("Файл настроек не является структурой: %1", ПутьКФайлуСНастройками);
	КонецЕсли;

КонецПроцедуры

Процедура Инициализация()

	ПутьКПлатформе = "";
	База        = Новый База();
	Хранилище   = Новый Хранилище();
	Репозиторий = Новый Репозиторий();

КонецПроцедуры